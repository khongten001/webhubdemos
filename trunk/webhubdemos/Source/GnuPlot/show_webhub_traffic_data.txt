# webhubdemos\Source\GnuPlot\show_webhub_traffic_data.txt
# usage: gnuplot show_webhub_traffic_data.txt

reset
set term png truecolor
set terminal pngcairo size 1024,768 enhanced font 'Segoe UI,10'
set output "webhub_traffic.png"
set style fill solid 1.00
set style histogram rowstacked
set style data histograms
set xlabel "WebHub AppID"
set ylabel "Page Requests per Instance; Total per AppID"

set key outside
set key right top

set boxwidth 0.8
#set yrange [0:4000]

# extract the 'total' cell
# http://stackoverflow.com/questions/18583180/gnuplot-how-to-load-and-display-single-numeric-value-from-data-file/18587275

stats 'WebHub_Traffic.dat' every ::::0 using 2 nooutput
total = int(STATS_min)

# columns 3 to 65 have traffic data
# data starts in row 1 (after the total)
# AppID is in column 1 -- display that on x axis
# Total for AppID also starts on row 1; display that at top of each bar

plot for [i=3:65] 'WebHub_Traffic.dat' every ::1 using i:xticlabels(1) with histogram \
notitle, '' \
every ::1 using 0:2:2 \
with labels offset 0,0.6 \
title sprintf("WebHub Traffic\nTotal Hits: %d", total)

#pause 10